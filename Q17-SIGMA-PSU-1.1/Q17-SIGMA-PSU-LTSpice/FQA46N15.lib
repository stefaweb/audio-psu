*FQA46N15 150V N-CHANNEL DMOSFET ELECTRICAL PARAMETERS
*------------------------------------------------------------------------------------
.SUBCKT  FQA46N15   20  10  30
Rg     10    1    1
M1      2    1    3    3    DMOS    L=1u   W=1u
.MODEL DMOS NMOS (VTO={3.62*{-0.00097*TEMP+1.02425}}  KP={38.0*{-0.000095*TEMP+1.002375}}
+ THETA=0.056  VMAX=1.35E5  LEVEL=3)
Cgs     1    3    2400p
Rd      20   4    15m      TC=0.013
Dds     3    4    DDS
.MODEL     DDS    D(BV={150*{0.00896*TEMP+0.776}} M=0.5  CJO=420p  VJ=0.8)
Dbody   3    20   DBODY
.MODEL   DBODY    D(IS=9.2E-13   N=1.0   RS=7.8m   EG=1.07  TT=130n)
Ra      4    2    9.6m     TC=0.013
Rs      3    5    0.3m     
Ls      5    30   0.55n
M2      1    8    6    6   INTER
E2      8    6    4    1   2
.MODEL   INTER    NMOS(VTO=0   KP=10   LEVEL=1)
Cgdmax  7    4    5385p
Rcgd    7    4    10meg
Dgd     6    4    DGD
Rdgd    4    6    10meg
.MODEL     DGD    D(M=0.85  CJO=5385p   VJ=0.5
M3      7    9    1    1   INTER
E3      9    1    4    1   -2
.ENDS FQA46N15 
*------------------------------------------------------------------------------------
*FAIRCHILD      CASE: TO-3P      PID: FQA46N15 
*MAY-02-2002 CREATION     
